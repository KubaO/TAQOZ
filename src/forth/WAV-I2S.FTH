{
WAVE FILE PLAYBACK FOR PROPELLER 2 WITH ADAFRUIT MAX98357 I2S CLASS D MONO AMPLIFIER

CLOCK SETUP AT 80MHZ
16 KHZ SAMPLE RATE, 16 BITS RESOLUTION
BCLK FREQUENCY : 16KHZ * 16 (BITS) * 2 (LEFT/RIGHT) = 512KHZ

BCLK AND LRCLK GENERATED WITH 2 SMARTPINS IN NCO FREQUENCY MODE
WYPIN SET AT 2**32/2 = $8000_0000
WXPIN FOR BCLK : (80E6)/(512E3)/2 = 78.125 APPROXIMATED TO 78
WXPIN FOR LRCLK : 78*32 = 2496

DIN SETUP IN SYNCHRONOUS SERIAL MODE
IF YOU CHANGE DIN AND BCLK PINS, YOU NEED TO CHANGE DIN MODE CONFIG
DIN CLOCK PIN IS SETUP AT PIN + 2

LINKS :
https://learn.adafruit.com/adafruit-max98357-i2s-class-d-mono-amp/pinouts
https://cdn-learn.adafruit.com/downloads/pdf/adafruit-max98357-i2s-class-d-mono-amp.pdf
https://cdn-shop.adafruit.com/product-files/3006/MAX98357A-MAX98357B.pdf
http://soundfile.sapp.org/doc/WaveFormat/
}

--- ASSIGN PINS
14		:= *LRCLK
12		:= *BCLK
10		:= *DIN

--- SMARTPINS CONSTANTS
%1010 24 << %1_11100_0 OR	:= #SYNC_TX
%1_00110_0			:= #NCO_FREQ
1 14 << NCO_FREQ OR		:= #!NCO_FREQ
%001111				:= #CONT-16BIT

: DISABLE_I2S			*LRCLK LOW *BCLK LOW *DIN LOW ;

: ENABLE_I2S			*LRCLK HIGH *BCLK HIGH *DIN HIGH ;


--- SMARTPINS SETUP
: !PINS
	DISABLE_I2S
	*LRCLK PIN #NCO_FREQ WRPIN 2496 WXPIN $8000_0000 WYPIN
	*BCLK PIN #!NCO_FREQ WRPIN 78 WXPIN $8000_0000 WYPIN
	*DIN PIN #SYNC_TX WRPIN #CONT-16BIT WXPIN
	;

pre PLAY ( <filename> -- )
	[C] FOPEN
---	WAVE DATA START AT ADDRESS 44, PRIME SYNC SERIAL SHIFTER
	44 DUP SDW@ REV 16>> DUP 4* WYPIN DUP 14 >> SWAP
	ENABLE_I2S
	DUP -ROT 4* OR WYPIN 14 >> SWAP 2+ WAITPIN
---	ADDRESS 40 IS SUBCHUNK2SIZE
	40 SD@ 2/ 2 - ( subchunksize )
---	EXIT ON KEY PRESS OR EOF
	FOR
	  DUP SDW@ REV 16>> SWAP -ROT DUP -ROT 4* OR WYPIN WAITPIN
	  DUP 14 >> SWAP DUP -ROT 4* OR WYPIN 14 >> SWAP 2+ WAITPIN
	  KEY
	?NEXT
	;